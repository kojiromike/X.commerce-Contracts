grammar com.x.xocl.XOCL with org.eclipse.xtext.common.Terminals

generate domainmodel "http://xocl.x.com/XOCL"

DomainModel: (elements += Package)*;

Package:
    _PACKAGE name = PackageName (version = VersionString)?
    LBRACE
        (members += PackageMember)+
    RBRACE
;

PackageName: QualifiedName;

QualifiedName: ID (PERIOD ID)*;

VersionString: VERSION;

PackageMember:
	Import |
    Package | 
    Contract | 
    Entity | 
    Process | 
    NamedWorkflow | 
    Transaction | 
    NamedType
;

Import: _IMPORT importedNamespace = QualifiedNameWithWildcard;

QualifiedNameWithWildcard: PackageName '.*'?;

Contract:
    _CONTRACT name = ID (EXTENDS extends = [Contract | QualifiedName])?
    LBRACE
        DESCRIPTION description = STRING
        ROLES
    	LBRACE
    		(roles += [ProcessRole | QualifiedName] (LANGLE (variants += ProcessVariant) (COMMA variants += ProcessVariant)* RANGLE)?)+
    	RBRACE
    RBRACE
;

Process:
    _PROCESS name = ID
    (LANGLE (variants += ProcessVariant) (COMMA variants += ProcessVariant)* RANGLE)?
    (LPAREN (parameters += ProcessParameter) (COMMA (parameters += ProcessParameter))* RPAREN)?
    LBRACE
        DESCRIPTION description = STRING
        ROLES LPAREN (roles += ProcessRole) (COMMA (roles += ProcessRole))+ RPAREN
    	(preconditions = PreconditionsClause)?
    	(workflow = AnonymousWorkflow)?
    	(postconditions = PostconditionsClause)?
    RBRACE
;

ProcessVariant: name = ID;

ProcessParameter: Parameter;

Parameter: type = [Entity | QualifiedName] name = ParameterName;

ParameterName: ID;

ProcessRole: Role;
 
Role: name = ID;

PreconditionsClause:
    PRECONDITIONS LPAREN (conditions += ParameterExpression) (COMMA conditions += ParameterExpression)* RPAREN
;

ParameterExpression:
	value = ParameterName LBRACK state = [EntityState | QualifiedName] RBRACK
;

Entity:
    _ENTITY name = ID 
    LBRACE
        (states += EntityState)+
    RBRACE
;

EntityState:
    STATE name = ID 
    (LBRACK (transitions += EntityStateTransition) (COMMA transitions += EntityStateTransition)* RBRACK)?
;
 
EntityStateTransition: successor = [EntityState];

PostconditionsClause:
    POSTCONDITIONS {PostconditionsClause}
    LBRACE
        (success = OnProcessSuccess)?
        (failure = OnProcessFailure)?
    RBRACE
;

OnProcessResult: OnProcessSuccess | OnProcessFailure;

OnProcessSuccess:
    SUCCESS LPAREN (conditions += ParameterExpression) (COMMA conditions += ParameterExpression)* RPAREN
;

OnProcessFailure:
    FAILURE LPAREN (conditions += ParameterExpression) (COMMA conditions += ParameterExpression)* RPAREN
;

AnonymousWorkflow: 
	_WORKFLOW 
	contents = WorkflowBody
;

NamedWorkflow:
    _WORKFLOW name = ID 
    IMPLEMENTS implements = [Process | QualifiedName] 
    contents = WorkflowBody
;

WorkflowBody: 
	{WorkflowBody}
    LBRACE
        (elements += WorkflowElement)*
        (elements += EndPoint)?
    RBRACE
;

EndPoint: SuccessEndpoint | FailureEndpoint;

SuccessEndpoint: name = SUCCESS;

FailureEndpoint: name = FAILURE;

WorkflowElement: AnyElement | AllElement | IfElement | ActionElement;

AnyElement:
    ANY
    LBRACE
    	(elements += WorkflowElement)+
        (elements += SuccessEndpoint)?
    RBRACE
;

AllElement:
    ALL
    LBRACE
    	(elements += WorkflowElement)+
        (elements += FailureEndpoint)?
    RBRACE
;

IfElement:
	{IfElement}
    IF 
    LPAREN (expression = ParameterExpression)? RPAREN
    LBRACE
        (elements += WorkflowElement)*
        (elements += EndPoint)?
    RBRACE
;

BooleanExpression: STRING;

ActionElement: NoopAction | TransactionCall | WorkflowCall;

NoopAction: {NoAction} NOOP;

TransactionCall:
    _TRANSACTION name = [Transaction | QualifiedName]
    (LANGLE (variants += TransactionVariantMapping) (COMMA variants += TransactionVariantMapping)* RANGLE)?
    (LPAREN (arguments += TransactionArgument) (COMMA (arguments += TransactionArgument))* RPAREN)?
    LBRACE
        ROLES LPAREN (sender = SenderRoleMapping) COMMA (receiver = ReceiverRoleMapping) RPAREN
    RBRACE
;

TransactionVariantMapping: value = [ProcessVariant | QualifiedName];

TransactionArgument: value = [ProcessParameter | QualifiedName];

SenderRoleMapping: value = [ProcessRole| QualifiedName];

ReceiverRoleMapping: value = [ProcessRole| QualifiedName];

WorkflowCall:
    _WORKFLOW name = [NamedWorkflow | QualifiedName]
    (LANGLE (variants += WorkflowVariantMapping) (COMMA variants += WorkflowVariantMapping)* RANGLE)?
    (LPAREN (arguments += WorkflowArgument) (COMMA (arguments += WorkflowArgument))* RPAREN)?
    LBRACE
        ROLES LPAREN (roles += WorkflowRoleMapping) (COMMA roles += WorkflowRoleMapping)+ RPAREN
    RBRACE
;

WorkflowArgument: value = [ProcessParameter | QualifiedName];

WorkflowVariantMapping: value = [ProcessVariant | QualifiedName];

WorkflowRoleMapping: value = ([ProcessRole | QualifiedName]);

Transaction:
    _TRANSACTION name = ID
    (LANGLE (variants += TransactionVariant) (COMMA variants += TransactionVariant)* RANGLE)?
    (LPAREN (parameters += TransactionParameter) (COMMA (parameters += TransactionParameter))* RPAREN)?
    body = (InformTransaction | NotifyTransaction | ResponseTransaction)
;

TransactionVariant: name = ID;

TransactionParameter: Parameter;

InformTransaction:
    pattern = (INFORM | INFORMATION | EVENT)
    (body = InformBody)?
;

InformBody:
    LBRACE
    	SENDER (sender = STRING)? sendAction = InformAction
    	(RECEIVER)?
     	(postconditions = PostconditionsClause)?
    RBRACE
;

InformAction:
    LBRACE {InformAction}
		(messages += InformMessage)?
    	(variants += InformVariant)*
    RBRACE	
;

InformMessage:
	MESSAGE {InformMessage}
	(type = [RecordType | QualifiedName])?
	(options = InformOptions)?	
;

InformVariant:
	MESSAGE 
	LANGLE variant = [TransactionVariant] RANGLE 
	(type = [RecordType | QualifiedName])?
	(options = InformOptions)?
;

InformOptions:
	LBRACE
		requires = RequiredFeatures
	RBRACE
;

RequiredFeatures:
    REQUIRES LPAREN (features += RequiredFeature) (COMMA (features += RequiredFeature))? RPAREN
;

RequiredFeature: AuthorizationFeature | NonRepudiationFeature | ConfidentialityFeature | IntegrityFeature;

AuthorizationFeature: authorization ?= AUTHORIZATION;

NonRepudiationFeature: nonrepudiation ?= NONREPUDIATION;

ConfidentialityFeature: confidentiality ?= CONFIDENTIALITY;

IntegrityFeature: integrity ?= INTEGRITY;

NotifyTransaction:
    pattern = (NOTIFY | NOTIFICATION)
    (body = NotifyBody)?
;

NotifyBody:
    LBRACE
 		SENDER (sender = STRING)? sendAction = NotifyAction
 		(RECEIVER)?
    	(postconditions = PostconditionsClause)?
    RBRACE
;

NotifyAction: 
    LBRACE {NotifyAction}
    	(messages += NotifyMessage)?
    	(variants += NotifyVariant)*
    RBRACE
;

NotifyMessage:
	MESSAGE {NotifyMessage}
	(type = [RecordType | QualifiedName])?
	(options = NotifyOptions)?
;

NotifyVariant:
	MESSAGE 
	LANGLE variant = [TransactionVariant] RANGLE 
	(type = [RecordType | QualifiedName])?
	(options = NotifyOptions)?
;

NotifyOptions:
	LBRACE {NotifyOptions}
		(requires = RequiredFeatures)?
		(timeouts = NotifyTimeOuts)?
		(RETRIES EQUALS retries = INT)?
	RBRACE
;

NotifyTimeOuts:
    TIMEOUTS LPAREN (timeouts += NotifyTimeOut) (COMMA (timeouts += NotifyTimeOut))? RPAREN
;

NotifyTimeOut: ReceiptAckTimeOut | ValidationAckTimeOut;

ReceiptAckTimeOut: RECEIPT EQUALS timeout = TimeOut;

ValidationAckTimeOut: VALIDATION EQUALS timeout = TimeOut;

TimeOut: value = INT unit = TimeUnit;

TimeUnit: 
	(unit = SECONDS) | 
	(unit = MINUTES) | 
	(unit = HOURS) | 
	(unit = DAYS)
;

Pattern:
    (pattern = RESPONSE) | 
    (pattern = CONFIRM) | (pattern = CONFIRMATION) |
    (pattern = QUERY) | 
    (pattern = OBLIGATION)
;

ResponseTransaction:
    pattern = Pattern
    (body = ResponseBody)?
;

ResponseBody:
    LBRACE {ResponseBody}
 		(SENDER (sender = STRING)? sendAction = SendAction)?
 		(RECEIVER (receiver = STRING)? receiveAction = ReceiveAction)?
    	(postconditions = PostconditionsClause)?
    RBRACE
;

SendAction: 
    LBRACE {SendAction}
    	(messages += SendMessage)?
    	(variants += SendVariant)*
    RBRACE
;

SendMessage:
	MESSAGE {SendMessage}
	(type = [RecordType | QualifiedName])?
	(options = SendOptions)?
;

SendVariant:
	MESSAGE 
	LANGLE variant = [TransactionVariant] RANGLE 
	(type = [RecordType | QualifiedName])?
	(options = SendOptions)?
;

SendOptions:
	LBRACE {SendOptions}
		(requires = RequiredFeatures)?
		(timeouts = SendTimeOuts)?
		(RETRIES EQUALS retries = INT)?
	RBRACE
;

SendTimeOuts:
    TIMEOUTS LPAREN (timeouts += SendTimeOut) ((COMMA (timeouts += SendTimeOut)) (COMMA (timeouts += SendTimeOut))?)? RPAREN
;

SendTimeOut: NotifyTimeOut | ResponseTimeOut;

ResponseTimeOut: RESPONSE EQUALS timeout = TimeOut;

ReceiveAction:
	LBRACE {ReceiveAction}
		(messages += SuccessMessage)?
    	(variants += SuccessVariant)*
    	(messages += FailureMessage)?
    	(variants += FailureVariant)*
	RBRACE
;

SuccessMessage:
	SUCCESS {SuccessMessage}
	(type = [RecordType | QualifiedName])?
	(options = NotifyOptions)?
;

SuccessVariant:
	SUCCESS 
	LANGLE variant = [TransactionVariant] RANGLE 
	(type = [RecordType | QualifiedName])?
	(options = NotifyOptions)?
;

FailureMessage:
	FAILURE {FailureMessage}
	(type = [RecordType | QualifiedName])?
	(options = NotifyOptions)?
;

FailureVariant:
	FAILURE 
	LANGLE variant = [TransactionVariant] RANGLE 
	(type = [RecordType | QualifiedName])?
	(options = NotifyOptions)?
;

PrimitiveType:
    (name = VOID) | 
    (name = VARCHAR) |    
    (name = BOOLEAN) | 
    (name = INTEGER) | 
    (name = LONG) | 
    (name = FLOAT) | 
    (name = DOUBLE) | 
    (name = GUID) | 
    (name = DATETIME) | 
    (name = BYTES)
;

FixedType: FIXED LPAREN length = INT RPAREN;

UnionType: UNION LBRACE (members += UnionMember) (COMMA (members += UnionMember))+ RBRACE;

Member: PrimitiveType | FixedType | CollectionType | NamedTypeReference;

UnionMember: Member | NullMember;

NullMember: name = NULL;

CollectionType: MapType | ArrayType;

MapType: MAP LANGLE type = CollectionMember RANGLE;

ArrayType: ARRAY LANGLE type = CollectionMember RANGLE;

CollectionMember: Member | UnionType;

NamedTypeReference: type = [NamedType | QualifiedName];

NamedType: EnumType | RecordType;

EnumType:
	ENUM name = ID
	LBRACE
		((members += EnumLiteral)(COMMA)?)*
	RBRACE
;

EnumLiteral: ID;

RecordType:
    RECORD name = ID (EXTENDS extends = [RecordType | QualifiedName])?
    body = PropertyList
;

PropertyList: 
	{PropertyList}
    LBRACE
        ((properties += Property)(SEMICOLON)?)*
    RBRACE
;

Property: PrimitiveProperty | FixedProperty | CollectionProperty | EnumProperty | NamedTypeProperty | UnionProperty;

PropertyName: ID;

PrimitiveProperty:
    (type = VOID (optional ?= QMARK)? name = PropertyName) | 
    (type = VARCHAR (optional ?= QMARK)? name = PropertyName (EQUALS sdefault = (STRING | NULL))?) |    
    (type = BOOLEAN (optional ?= QMARK)? name = PropertyName (EQUALS sdefault = TRUEFALSE)?) | 
    (type = INTEGER (optional ?= QMARK)? name = PropertyName (EQUALS ndefault = INT)?) | 
    (type = LONG (optional ?= QMARK)? name = PropertyName (EQUALS ndefault = INT)?) | 
    (type = FLOAT (optional ?= QMARK)? name = PropertyName (EQUALS sdefault = FLOATVALUE)?) | 
    (type = DOUBLE (optional ?= QMARK)? name = PropertyName (EQUALS sdefault = FLOATVALUE)?) | 
    (type = GUID (optional ?= QMARK)? name = PropertyName (EQUALS sdefault = (NEW | NULL))?) | 
    (type = DATETIME (optional ?= QMARK)? name = PropertyName (EQUALS sdefault = (NOW | NULL))?) | 
    (type = BYTES (optional ?= QMARK)? name = PropertyName)
;

FixedProperty: type = FixedType (optional ?= QMARK)? name = PropertyName (EQUALS sdefault = NULL)?;

CollectionProperty: type = CollectionType (optional ?= QMARK)? name = PropertyName (EQUALS sdefault = NULL)?;

EnumProperty: type = [EnumType | QualifiedName] (optional ?= QMARK)? name = PropertyName EQUALS default = EnumLiteral?;

NamedTypeProperty: type = [NamedType | QualifiedName] (optional ?= QMARK)? name = PropertyName (EQUALS sdefault = NULL)?;

UnionProperty: type = UnionType (optional ?= QMARK)? name = PropertyName (EQUALS default = NULL)?;

// Values

FLOATVALUE: INT (PERIOD INT)?;

VERSION: INT (PERIOD INT) (PERIOD INT)?;

NULL: 'null';
TRUEFALSE: 'true' | 'false';
NEW: 'new';
NOW: 'now';

// Types
ARRAY: 'array';
BOOLEAN: 'boolean';
DOUBLE: 'double';
ENUM: 'enum';
FLOAT: 'float';
INTEGER: 'int';
LONG: 'long';
MAP: 'map';
BYTES: 'bytes';
FIXED: 'fixed';
VARCHAR: 'string';
UNION: 'union';
VOID: 'void';
GUID: 'guid';
DATETIME: 'datetime';

// Keywords
EXTENDS: 'extends';
_PACKAGE: 'package';
_IMPORT: 'import';
RECORD: 'record';
_CONTRACT: 'contract';
ROLES: 'roles';
DESCRIPTION: 'description';
_PROCESS: 'process';
STATE: 'state';
POSTCONDITIONS: 'postconditions';
PRECONDITIONS: 'preconditions';
_ENTITY: 'entity';
SUCCESS: 'success';
FAILURE: 'failure';
ANY: 'any';
ALL: 'all';
IF: 'if';
NOOP: 'noop';
MESSAGE: 'message';
RESPONSE: 'response';
TIMEOUTS: 'timeouts';
RETRIES: 'retries';
SENDER: 'sender';
RECEIVER: 'receiver';
INFORM: 'inform';
INFORMATION: 'information';
EVENT: 'event';
NOTIFY: 'notify';
NOTIFICATION: 'notification';
CONFIRM: 'confirm';
CONFIRMATION: 'confirmation';
QUERY: 'query';
OBLIGATION: 'obligation';
REQUIRES: 'requires';
AUTHORIZATION: 'authorization';
IMPLEMENTS: 'implements';
NONREPUDIATION: 'nonrepudiation';
CONFIDENTIALITY: 'confidentiality';
INTEGRITY: 'integrity';
_TRANSACTION: 'transaction';
_WORKFLOW: 'workflow';
RECEIPT: 'receipt';
VALIDATION: 'validation';
SECONDS: 's';
MINUTES: 'm';
HOURS: 'h';
DAYS: 'd';

// Separators
LPAREN: '(';
RPAREN: ')';
LBRACE: '{';
RBRACE: '}';
LBRACK: '[';
RBRACK: ']';
LANGLE: '<';
RANGLE: '>';
COLON:  ':';
QMARK:  '?';
SEMICOLON: ';';
COMMA: ',';
AT: '@';
EQUALS: '=';
PERIOD: '.';
